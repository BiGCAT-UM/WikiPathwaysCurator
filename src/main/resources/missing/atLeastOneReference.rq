PREFIX cur: <http://vocabularies.wikipathways.org/wp#Curation:>
PREFIX wp:      <http://vocabularies.wikipathways.org/wp#>
PREFIX dc:      <http://purl.org/dc/elements/1.1/>
PREFIX foaf:    <http://xmlns.com/foaf/0.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>

SELECT DISTINCT ?homepage (str(?titleLit) as ?title) (str(?speciesLit) as ?species)
WHERE {
  {
    SELECT DISTINCT ?pathway (COUNT(?pubmed) AS ?count) WHERE {
      ?pathway wp:ontologyTag cur:AnalysisCollection .
      OPTIONAL {
        ?pubmed a                wp:PublicationReference ;
                dcterms:isPartOf ?pathway
      }
    } GROUP BY ?pathway
  }
  ?pathway dc:title ?titleLit ;
           wp:organismName ?speciesLit ;
           foaf:page ?homepage .
  FILTER (?count = 0)
} ORDER BY ?species ?title
